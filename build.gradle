buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
	  classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:6.1.0"
	}
}

plugins {
	id "org.ajoberstar.grgit" version "4.1.1-rc.4"
	id 'org.ajoberstar.git-publish' version '3.0.0'
	id "com.jfrog.bintray" version "1.8.4"
//	id "com.jfrog.artifactory" version "4.9.0"
}

apply plugin: 'biz.aQute.bnd.workspace'

ext {
	isTravisBuild = System.getenv().get("TRAVIS") == 'true'
	isJitPackBuild = System.getenv().get("JITPACK") == 'true'
}

// Prepare github authentication for plugins
if (System.properties['org.ajoberstar.grgit.auth.username'] == null) {
	System.setProperty('org.ajoberstar.grgit.auth.username',
		System.getenv("GH_TOKEN") ?: project.properties['github.token'] ?: "nouser")
}

subprojects {
	apply from: "${project.rootDir}/gradle/subprojects.gradle"
}

gitPublish {
	enabled = JavaVersion.current() == JavaVersion.VERSION_11

	repoUri = 'https://github.com/mnlipp/de.mnl.osgi.git'
	branch = 'gh-pages'
	contents {
		subprojects.each { subproject ->
			def javadocTask = subproject.tasks.findByName("javadoc")  
			if (javadocTask) {
				from(javadocTask) {
						into subproject.name + '/javadoc-latest'
				}
				// Releases only
				if (!subproject.version.contains("SNAPSHOT")) {
					from(javadocTask) {
						into subproject.name + '/javadoc'
					}
				}
			}
		}
	}
	preserve { include '**/*' }
	commitMessage = "Updated."
}

task stage {
	description = 'To be executed by github actions, build and update JavaDoc.'
	group = 'build'

	// Build everything first
	subprojects.each { subproject ->
		def javadocTask = subproject.tasks.findByName("javadoc")  
		if (javadocTask) {
			dependsOn javadocTask
		}
	}
	
   	dependsOn gitPublishPush
}

apply plugin: 'eclipse'
tasks.eclipse.dependsOn(cleanEclipse)
