// Additional jars for publishing
apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}
 
task javadocJar(type: Jar) {
	from (javadoc)
	classifier "javadoc"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// MavenPublishing (new)
    
publishing {
        
    repositories {
        maven {
            name "snapshot"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username project.properties['sonatypeUsername'] ?: "nouser"
                password project.properties['sonatypePassword'] ?: "nopass"
            }
        }
        maven {
            name "release"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username project.properties['sonatypeUsername'] ?: "nouser"
                password project.properties['sonatypePassword'] ?: "nopass"
            }
        }
        maven {
            name "ojo"
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
            credentials {
				username = project.properties['bintrayUser'] ?: "nouser"
				password = project.properties['bintrayApiKey'] ?: "nokey"
            }
        }
    }
}

// Additional configuration of publishing
apply plugin: ConfigurePublishing

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from generatePomFileForMavenPublication
        rename ".*", "pom.xml"
    }
}

task releaseTag {
    doLast {
        String name = project.name.startsWith(project.group + ".") ?
            project.name.substring(project.group.length() + 1) : project.name
        name = name.replace('.', '-')
        String tag = name + "-" + project.version
        grgit.tag.add(name: tag, message: "Released.")
        grgit.push(refsOrSpecs: [tag])
    }
}

build.mustRunAfter "releaseTag"

task releaseToOss {
    group = "publishing"
    
    dependsOn "releaseTag"
    dependsOn "publishMavenPublicationToReleaseRepository"
}
