
group = "de.mnl.osgi"
// Default, override in subprojects
project.ext.licenses = ['AGPL-3.0']

repositories {
	mavenCentral()
}

if (plugins.hasPlugin('biz.aQute.bnd')) {

	compileJava {
		sourceCompatibility = 11
		targetCompatibility = 11
	}

	// artifact_version is defined in cnf/bnd.bnd
	if (bnd.artifact_version) {
		project.version = bnd.artifact_version
	}
	if (bnd["-groupid"]) {
		group = bnd["-groupid"]
	}
	if (bnd["Bundle-Description"]) {
		description = bnd["Bundle-Description"]
	} else {
		if (bnd["Bundle-Name"]) {
			description = bnd["Bundle-Name"]
		}
	}

	dependencies {
		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
	}

	test {
		useJUnitPlatform()
	}
	
	if (tasks.findByName("javadoc")) {
		javadoc {
			options.addStringOption("doctitle", "${project.name} ${project.version}")
			options.addBooleanOption("use", true)
			options.addBooleanOption("linksource", true)
			options.addStringOption("bottom", file("${rootProject.rootDir}/misc/javadoc.bottom.txt").text)
			options.links "https://docs.oracle.com/en/java/javase/11/docs/api/"
			options.links "https://osgi.org/javadoc/osgi.core/7.0.0/"
			options.links "https://osgi.org/javadoc/osgi.cmpn/7.0.0/"
			// Add --allow-script-in-comments if available (since 1.8.0_121)
			options.addBooleanOption('-allow-script-in-comments').value = true
		}
	}
}

apply from: "${project.rootDir}/gradle/eclipse.gradle"
